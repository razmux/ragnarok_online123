//===== rAthena Script =======================================
//= Eden Group Quests - Common NPCs
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 2.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Eden Group Headquarter NPC's.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Removed showevent use, and use of duplicates.
//=     Commented out warps that should not be active.
//= 1.2 Reduced 300+ copy-paste lines to 1 duplicated NPC. [Brian]
//= 1.3 Some little optimization here and there. [Masao]
//= 1.4 Updated to match the new Izlude Map. [Masao]
//= 1.5 Added some missing Eden Group Teleport Officers. [Masao]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Added Malaya teleporter. [Euphy]
//= 1.8 Updated to match the latest official script. [Euphy]
//= 1.9 Added support to Para_Team_Mark_ in Eden Group. [AtlantisRO/Ragno]
//= 2.0 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

// Common NPCs :: lv_tre
//============================================================
moc_para01,27,35,5	script	Secretary Lime Evenor	952,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 2000) {
		mes "- Espera un minuto !! -";
		mes "- Actualmente estás cargando -";
		mes "- demasiados items contigo. -";
		mes "- Porfavor intenta nuevamente -";
		mes "- despues de haber perdido algo de peso. -";
		close;
	}
	if (countitem(6219) > 0) {
		mes "[Lime Evenor]";
		mes "Gracias a muchos aventureros que visitaron nuestro Eden Group, hemos preparado recientemente una nueva version de Eden Group Mark para nuestros visitantes..";
		mes "Y tambien es posible cambiar la version anterior de Eden Group por una nueva.";
		next;
		mes "[Lime Evenor]";
		mes "Aunque tiene un ligero retraso, esta es la nueva version de Eden Group Mark lo enviara de vuelta a la ubicacion guardada actualmente.";
		mes "Te gustaría intercambiar los tuyos?";
		next;
		if (select("Bueno:Hmm estoy bien.") == 2){
			mes "[Lime Evenor]";
			mes "Que posible razones tendrias... para no intercambiar?";
			close;
		}
		delitem 6219,1;
		getitem 22508,1;
		mes "[Lime Evenor]";
		mes "Aqui tienes, vuelve luego~";
		close;
	}
	mes "[Lime Evenor]";
	mes "Chicos que buscan sus metas, escuchenme. Somos los representantes del paraiso llamado 'El jardin de Eden', no hay otro lugar como ese por aqui.";
	mes "Hola, en que te puedo ayudar?";
	next;
	while (1) {
		switch (select("Que es el Eden Group?:Unirse al Eden Group.:Registrar nueva mision.:Buscar misiones.:Cancelar.")) {
		case 1:
			mes "[Lime Evenor]";
			mes "Somos un grupo que ayudamos a personas que sufren la angustia dentro de Mystery RO y del Reino de Rune Midgard.	";
			next;
			mes "[Lime Evenor]";
			mes "Si las demas personas necesitan ayuda, anotamos su caso en un tablon de anuncios esperando que nuestros capacitados miembros puedan resolverlos.";
			next;
			mes "[Lime Evenor]";
			mes "Los miembros de Eden revisan esos tablones de anuncios dispuestos a ayudar.";
			mes "Cada mision tiene su recompensa.";
			next;
			mes "[Lime Evenor]";
			mes "Si no estas familiarizado con Rune Midgard, es una buena forma de comenzar a explorar este mundo.";
			mes "Ahora, si estas ya capacitado, igualmente puedes recorrer el mundo buscando camaradas y haciendo misiones, tu nombre sera honrado.";
			next;
			break;
		case 2:
			if (!callfunc("F_HasEdenGroupMark")) {
				mes "[Lime Evenor]";
				mes "Puedes ser un Miembro de Eden, solo tienes que registrarte conmigo.";
				mes "Te gustaria unirte al Eden Group?";
				next;
				switch (select("Si, Quiero entrar.:No, No me interesa.")) {
				case 1:
					mes "[Lime Evenor]";
					mes "Excelente!! Porfavor, escribe tu nombre aqui.";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "Listo?";
					mes "Dejame ver un segundo..";
					next;
					mes "[Lime Evenor]";
					mes "TU nombre es ^3131FFBo"+.@input$+"Ba^000000...? estoy en lo correcto?";
					mes "Huh? No es??";
					next;
					emotion ET_SWEAT;
					mes "[Lime Evenor]";
					mes "Oh, no fue lo que escribiste?";
					mes "Ok, hmmm, creo que debo ir al oculista.";
					next;
					mes "[Lime Evenor]";
					mes "Puedes escribir tu nombre denuevo? Por favor se ^3131FFClaro^000000.";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "Ah~ ha. Eres ^3131FF"+strcharinfo(0)+"^000000.";
					mes "Ahora si esta bien escrito.";
					next;
					emotion ET_BEST;
					mes "[Lime Evenor]";
					mes "Bien! Hemos puesto tu nombre en la lista.";
					mes "Ahora eres un miembro oficial del Eden Group! Bienvenido!";
					next;
					mes "[Lime Evenor]";
					mes "Esperamos que puedas ser de mucha ayuda por aqui!.";
					getitem 22508,1; //Para_Team_Mark_
					next;
					break;
				case 2:
					mes "[Lime Evenor]";
					mes "¿Aun tiene preguntas sobre Eden Group?";
					next;
					break;
				}
			} else {
				mes "[Lime Evenor]";
				mes "Ya eres miembro de Eden Group!.";
				next;
			}
			break;
		case 3:
			mes "[Lime Evenor]";
			mes "Desea registrar algunas misiones que quiere que hagamos?";
			next;
			if(select("Si, Me quiero registrar.:No, No me interesa.") == 1) {
				mes "[Lime Evenor]";
				mes "Excelente!! Porfavor, escribe tu nombre aqui.";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FF"+strcharinfo(0)+"^000000.. Ese es tu nombre?";
				mes "Hmm, tu escritura no esta clara. No puedo leer eso";
				next;
				mes "[Lime Evenor]";
				mes "^3131FF"+strcharinfo(0)+"^000000, dime que quieres registrar";
				mes "A que mapa deberiamos ir?";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMission Map: "+.@inputstr$+"^000000";
				mes "Hmm, Ya veo.";
				next;
				mes "[Lime Evenor]";
				mes "Por favor, hagame saber que tipo de misiones este buscando.";
				mes "Deberia ser rapido, algo asi como cazar 10 porings.'";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "Ya terminaste?";
				mes "Dejame ver.";
				next;
				mes "[Lime Evenor]";
				mes "Huh...........";
				next;
				mes "[Lime Evenor]";
				mes "Hum.. huh??..................";
				next;
				emotion ET_THINK;
				mes "[Lime Evenor]";
				mes "Creo que deberias completar una de nuestras misiones antes de comenzar una nueva mision.";
				next;
				emotion ET_HNG;
				mes "[Lime Evenor]";
				mes "^3131FFClient: Lime Evenor^000000";
				mes "^3131FFMission: Practica tu escritura por un mes.^000000";
				mes "Tienes una mala caligrafia!";
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMission:"+.@input$+"^000000";
				mes "De todos modos, has terminado de registrarte para una nueva mision.";
				next;
				mes "[Lime Evenor]";
				mes "Tu lista de misiones esta llena. Deberias limpiarla un poco si quieres seguir.";
				mes "Por favor espere su turno para ser atendido.";
				next;
			}
			break;
		case 4:
			if (callfunc("F_HasEdenGroupMark")) {
				mes "[Lime Evenor]";
				mes "¿Le gustaria probar algunas misiones como miembro de Eden";
				next;
				mes "[Lime Evenor]";
				mes "Mira el lado derecho del tablero de anuncios, hay varios tipos de misiones esperandote.";
				mes "Lea detenidamente la lista de misiones y las calificaciones y elija una de ellas.";
				next;
			}
			else {
				mes "[Lime Evenor]";
				mes "Debes unirte al grupo Eden primero si quieres hacer algunas misiones.";
				close;
			}
			break;
		case 5:
			mes "[Lime Evenor]";
			mes "Si tiene alguna pregunta, por favor vuelva otra vez.";
			close;
		}
	}
	close;
}

-	script	Eden Teleport Officer#0::eto	-1,{
	mes "[Eden Teleport Officer]";
	mes "Aquellos que deambulan por este el mundo de Mystery RO con ojos sin rumbo escuchen y vengan a mi!";
	mes "Eden Group esta aqui para ayudarlos y les mostrara el gran futuro.";
	next;
	mes "[Eden Teleport Officer]";
	mes "No tienes nada que hacer mas que perder tu tiempo?";
	mes "Estas ansioso por hacer algo bueno pero nadie te ofrece trabajo?";
	mes "¿Le gustaria resolver problemas?";
	mes "Eden Group tiene la solucion a su problema!!.";
	next;
	switch (select("Llevame a Eden Group.:No me interesa, Gracias.")) {
	case 1:
		mes "[Eden Teleport Officer]";
		mes "¡Vamos a nuestra base secreta!";
		set nak_warp,strnpcinfo(2);
		close2;
		warp "moc_para01",31,14;
		end;
	case 2:
		mes "[Eden Teleport Officer]";
		mes "Si no sabes que hacer para tu futuro, no dudes en decirmelo. ¡Ven y habla conmigo! De acuerdo?";
		close;
	}
}
prontera,124,76,3	duplicate(eto)	Eden Teleport Officer#1	729
moc_ruins,68,164,3	duplicate(eto)	Eden Teleport Officer#2	729
geffen,132,66,3	duplicate(eto)	Eden Teleport Officer#3	729
alberta,124,67,3	duplicate(eto)	Eden Teleport Officer#4	729
aldebaran,133,119,5	duplicate(eto)	Eden Teleport Officer#5	729
izlude_in,68,162,1	duplicate(eto)	Eden Teleport Officer#6	729
prt_church,103,78,3	duplicate(eto)	Eden Teleport Officer#7	729
geffen_in,160,104,5	duplicate(eto)	Eden Teleport Officer#8	729
moc_prydb1,53,126,3	duplicate(eto)	Eden Teleport Officer#9	729
alberta_in,75,39,3	duplicate(eto)	Eden Teleport Officer#10	729
payon_in02,58,58,1	duplicate(eto)	Eden Teleport Officer#11	729
payon,177,111,3	duplicate(eto)	Eden Teleport Officer#12	729
que_ng,33,73,3	duplicate(eto)	Eden Teleport Officer#13	729
que_ng,144,161,5	duplicate(eto)	Eden Teleport Officer#14	729
yuno,144,189,5	duplicate(eto)	Eden Teleport Officer#15	729
rachel,125,144,3	duplicate(eto)	Eden Teleport Officer#16	729
comodo,202,151,4	duplicate(eto)	Eden Teleport Officer#17	729
hugel,93,153,4	duplicate(eto)	Eden Teleport Officer#18	729
veins,220,109,4	duplicate(eto)	Eden Teleport Officer#19	729
einbroch,250,211,4	duplicate(eto)	Eden Teleport Officer#20	729
lighthalzen,164,86,4	duplicate(eto)	Eden Teleport Officer#21	729
amatsu,100,145,5	duplicate(eto)	Eden Teleport Officer#22	729
ayothaya,221,191,3	duplicate(eto)	Eden Teleport Officer#23	729
louyang,224,107,3	duplicate(eto)	Eden Teleport Officer#24	729
gonryun,162,122,5	duplicate(eto)	Eden Teleport Officer#25	729
moscovia,209,197,5	duplicate(eto)	Eden Teleport Officer#26	729
brasilis,191,224,3	duplicate(eto)	Eden Teleport Officer#27	729
dewata,192,193,5	duplicate(eto)	Eden Teleport Officer#28	729
morocc,161,97,5	duplicate(eto)	Eden Teleport Officer#29	729
izlude,131,148,4	duplicate(eto)	Eden Teleport Officer#30	729
izlude_a,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_a	729
izlude_b,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_b	729
izlude_c,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_c	729
izlude_d,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_d	729
umbala,105,158,3	duplicate(eto)	Eden Teleport Officer#31	729
malaya,238,206,6	duplicate(eto)	Eden Teleport Officer#32	729

moc_para01,30,10,0	script	#eden_out	45,1,1,{
OnTouch:
	switch (nak_warp) {
		case 1: warp "prontera",116,72; end;
		case 2: warp "moc_ruins",64,161; end;
		case 3: warp "geffen",120,39; end;
		case 4: warp "alberta",117,56; end;
		case 5: warp "aldebaran",168,112; end;
		case 6: warp "izlude_in",73,165; end;
		case 7: warp "prt_church",99,78; end;
		case 8: warp "geffen_in",162,99; end;
		case 9: warp "moc_prydb1",51,118; end;
		case 10: warp "alberta_in",73,43; end;
		case 11: warp "payon_in02",64,60; end;
		case 12: warp "payon",161,58; end;
		case 13: warp "que_ng",33,63; end;
		case 14: warp "que_ng",144,166; end;
		case 15: warp "yuno",158,125; end;
		case 16: warp "rachel",115,125; end;
		case 17: warp "comodo",192,145; end;
		case 18: warp "hugel",88,148; end;
		case 19: warp "veins",216,104; end;
		case 20: warp "einbroch",246,204; end;
		case 21: warp "lighthalzen",159,95; end;
		case 22: warp "amatsu",110,150; end;
		case 23: warp "ayothaya",217,178; end;
		case 24: warp "louyang",217,103; end;
		case 25: warp "gonryun",155,120; end;
		case 26: warp "moscovia",218,198; end;
		case 27: warp "brasilis",190,220; end;
		case 28: warp "dewata",192,182; end;
		case 29: warp "morocc",161,97; end;
		case 30: warp "izlude",134,118; end;	// Old coordinates: (127,142)
		case 31: warp "umbala",94,154; end;
		case 32: warp "malaya",234,199; end;
		default: warp "prontera",116,72; end;
	}
	end;
}

moc_para01,47,39,3	script	#warp_2_pass	111,{
	if (callfunc("F_HasEdenGroupMark")) {
		mes "[Lime Evenor]";
		mes "Oh, es un lugar exclusivo solo para los miembros de Eden.";
		mes "Si eres miembro, puedes venir cuando quieras!";
		next;
		switch (select("Entrar.:No me interesa.")) {
		case 1:
			mes "Esta puerta esta bellamente decorada, pero parece un poco demasiado pesada.";
			close2;
			warp "moc_para01",106,14;
			end;
		case 2:
			mes "[Lime Evenor]";
			mes "Bueno, se notaba que no estabas interesado.";
			close;
		}
	}
	mes "[Lime Evenor]";
	mes "Oh, es un lugar exclusivo solo para los miembros de Eden.";
	mes "Si quieres entrar, tienes que unirte al Grupo Eden.";
	close;
}

moc_para01,16,22,7	script	Old Adventurer#eden	900,{
	mes "[Old Adventurer]";
	mes "Duro!, soy lo suficientemente fuerte incluso parezco viejo! Cual es su nombre? Lime Evenor? Simplemente me ignora como si fuera una especie de viejo bastardo senil.";
	next;
	mes "[Old Adventurer]";
	mes "Vine aqui para unirme al grupo Eden!, No creo merecer esto!";
	mes "Tambien crees que me veo como si estuviera senil?";
	close;
}

moc_para01,182,48,3	script	Eden's Chief#eden	886,{
	set .@eggf,rand(1,118);
	set .@eggf_1,.@eggf + 1;
	set .@eggf_2,.@eggf + 2;
	set .@eggf_6,.@eggf + 6;
	mes "[Eden's Chief]";
	mes ""+.@eggf+".. "+.@eggf_1+"... "+.@eggf_2+".... "+.@eggf_6+"..?";
	next;
	emotion ET_CRY;
	mes "[Eden's Chief]";
	mes "Maldicion! , Me hiciste perder la cuenta!, Cuantos huevos llevo?";
	mes "Aww !!! Lo olvide totalmente! Tengo que contar de nuevo!";
	next;
	emotion ET_CRY;
	mes "[Eden's Chief]";
	mes "Por que me has pedido que haga esto? ¿Que tipo de mision es esa??";
	next;
	emotion ET_CRY;
	mes "[Eden's Chief]";
	mes "¿Que vas a hacer con todos esos huevos fritos? Estoy seguro de que no puedes comerlos todos. Solo quieres molestarme, ¿verdad?";
	close;
}

moc_para01,172,28,5	script	Eden's Intern#eden	883,{
	mes "[Eden's Intern]";
	mes "En realidad, me di cuenta de que.";
	next;
	mes "[Eden's Intern]";
	mes "Tenemos que trabajar muy duro a menos que quieras fracasar...";
	next;
	mes "[Eden's Intern]";
	mes "Pero, nunca he aprendido sobre productos farmaceuticos.";
	next;
	mes "[Eden's Intern]";
	mes "Voy a fallar. No puedo hacer esto.";
	close;
}

moc_para01,20,35,5	script	Office Assistant Neede	814,{
	mes "[Neede]";
	mes "Me convertí en miembro de Eden hace un largo largo tiempo, pero pareciera como si fuera ayer.";
	mes "He estado haciendo un monton de trabajo, asi que ni siquiera noto cuanto tiempo llevo aqui.";
	next;
	mes "[Neede]";
	mes "Me recuerda que era como una guerra todos los dias.";
	next;
	mes "[Neede]";
	mes "El jefe ^3131FFdel restaurante del lado derecho^000000";
	mes "usa demasiado condimento cada vez que cocina, el olor esta en todo el vestibulo. Tenemos que abrir la ventana regularmente porque no podemos soportar el olor!";
	next;
	mes "[Neede]";
	mes "Y, ves el ^3131FFgimnasio^000000 en la cruz ^3131FFde el lado norte del looby^000000? Siempre gritan cuando hacen ejercicio, incluso puedo escucharlos desde aqui!";
	next;
	emotion ET_ANGER;
	mes "[Neede]";
	mes "Ademas, la chica ^3131FFdel segundo piso^000000 esta llorando todo el tiempo. Sus llantos me vuelven loco!!.";
	next;
	mes "[Neede]";
	mes "Ah, esa chica rara es en realidad nuestro jefe...";
	next;
	mes "[Neede]";
	mes "No se lo que ella ha estado haciendo. Pero ella llora y grita todo el tiempo. Oh, a veces escucho 'Bang!', ruidos fuertes ... No se de donde vienen.";
	next;
	emotion ET_HUK;
	mes "[Neede]";
	mes "Espera!";
	mes "Shh! Esto es un secreto, no se lo puedes contar a nadie!";
	next;
	mes "[Neede]";
	emotion ET_OHNO;
	mes "No confio en Evenor. Ella nunca muestra sus sentimientos... me va a delatar a mi jefe.";
	next;
	emotion ET_HNG, getnpcid(0, "Secretary Lime Evenor");
	mes "[Lime Evenor]";
	mes "No soy ese tipo de chico.";
	next;
	emotion ET_KEK;
	mes "[Neede]";
	mes "Huh? Se entero que..? Dios~!";
	close;
}

function	script	F_HasEdenGroupMark	{
	return (countitem(6219) > 0 || countitem(22508) > 0);
}
