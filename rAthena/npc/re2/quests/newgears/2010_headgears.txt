//===== rAthena Script =======================================
//= 2010 Headgear Quests
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Fairy Flower, Necktie, Hair Brush, Statue Of Baby Angel,
//= Frog Hat, Soloplay Boxes (Indifferent/Angry Solo Hat)
//=
//= Although the variables include '2009', this quest is
//= actually for 2010 headgears, and is Renewal-only.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//=     Original variable names: 2009_hat, 2009_1_hat
//============================================================

// Main Quest :: 2010_hat_0405
//============================================================
gonryun,149,189,0	script	Yotang#Flower_Of_Fairy	50,{
	mes "[Yotang]";
	mes "Allí vive un Hada, en el 3er Piso de Kunlun.";
	mes "Es muy famosa.. y muy hermosa.";
	mes "Incluso tiene un Fan club!";
	next;
	mes "[Yotang]";
	mes "Por supuesto que yo soy el presidente del club de fans.";
	mes "Quieres unirte a los ^B266FF[Fairy Fan Club]^000000?";
	next;
	if(select("No estoy interesado!!:Me uno ahora ya!!") == 1) {
		emotion et_surprise, 0;
		mes "[Yotang]";
		mes "Que!? No cuento contigo?";
		close;
	}
	if (countitem(7165) > 99 && countitem(7166) > 99 && countitem(745) > 0 && countitem(981) > 0 && countitem(2211) > 0) {
		mes "[Yotang]";
		mes "Oh.. los has traido!";
		mes "son los materiales para la <ITEM>Flower of Fairy<INFO>5437</INFO></ITEM>?";
		mes "Lo sabias? Era un secreto de Fan Club...";
		next;
		mes "[Yotang]";
		mes "Da igual, quieres crear la <ITEM>Flower of Fairy<INFO>5437</INFO></ITEM>?";
		next;
		mes "[Yotang]";
		mes "^FF0000Por Favor ten en cuenta";
		mes "que el refine de los materiales";
		mes "desaparecera al combinarlos.^000000";
		next;
		if(select("Mejor en otra ocasion.:Hazlo ahora mismo!!") == 1) {
			mes "[Yotang]";
			mes "Vuelve denuevo!";
			close;
		}
		callsub L_CheckWeight;
		mes "[Yotang]";
		mes "Eres rapido!";
		mes "Mereces ser el proximo Presidente de Fairy Fan Club.";
		next;
		emotion ET_SWEAT;
		mes "Mezclando todo... ... ...";
		next;
		if (countitem(7165) > 99 && countitem(7166) > 99 && countitem(745) > 0 && countitem(981) > 0 && countitem(2211) > 0) {
			callsub L_CheckWeight;
			mes "[Yotang]";
			mes "Listo!, la [Flower of Fairy].";
			delitem 7165,100; //Limpid_Celestial_Robe
			delitem 7166,100; //Soft_Silk_Cloth
			delitem 745,1; //Wedding_Bouquet
			delitem 981,1; //Violet_Dyestuffs
			delitem 2211,1; //Bandana
			getitem 5437,1; //Flower_Of_Fairy
			emotion ET_BEST;
			next;
			mes "[Yotang]";
			mes "Nos vemos en el Meet&Greed!";
			close;
		}
		mes "[Yotang]";
		mes "Huh? No tienes todo..";
		mes "te faltan materiales.";
		mes "No estaras usando la Bandana?";
		mes "Revisa!";
		next;
		mes "[Yotang]";
		mes "Traeme todos los materiales,";
		mes "si quieres obtener la gran <ITEM>Flower of Fairy<INFO>5437</INFO></ITEM>.";
		next;
		mes "[Yotang]";
		mes "Si me traes ^0000FF100 Transparent Celestial Robes, 100 Soft Silk Cloth,";
		mes "1 Wedding Bouquet, 1 Violet Dyestuffs";
		mes "y ^0000FF1 Bandana^000000 la cual encuentras en la tienda,";
		mes "podre crearte una <ITEM>[^0000FFFlower of Fairy^000000]<INFO>5437</INFO></ITEM>.";
		close;
	}
	mes "[Yotang]";
	mes "Puedes conocer al Hada pronto,";
	mes "hara un Meet&Greet con nosotros.";
	mes "Proximamente..";
	mes "La <ITEM>Flower of Fairy<INFO>5437</INFO></ITEM> es necesaria,";
	mes "para asistir al Meet&Greet. Todos la usaran!.";
	next;
	mes "[Yotang]";
	mes "Si me traes los materiales,";
	mes "Puedo hacer una para ti.";
	next;
	mes "[Yotang]";
	mes "Necesitaria ^0000FF100 Transparent Celestial Robes, 100 Soft Silk Cloth,";
	mes "1 Wedding Bouquet, 1 Violet Dyestuffs";
	mes "y ^0000FF1 Bandana^000000 que puedes encontrar en la tienda,";
	next;
	mes "[Yotang]";
	mes "Recuerda, ^0000FF100 Transparent Celestial Robes, 100 Soft Silk Cloth,";
	mes "1 Wedding Bouquet, 1 Violet Dyestuffs";
	mes "y ^0000FF1 Bandana^000000 que puedes encontrar en la tienda,";
	mes "Te hare la <ITEM>Flower of Fairy<INFO>5437</INFO></ITEM>.";
	next;
	mes "[Yotang]";
	mes "^FF0000Por favor ten en cuenta,";
	mes "el refine de los materiales";
	mes "desaparecera al combinarlos.^000000";
	next;
	if(select("No quiero hacerlo.:Toma! tengo los materiales!") == 1) {
		mes "[Yotang]";
		mes "Deberias!";
		close;
	}
	mes "[Yotang]";
	mes "Huh? Bromeas conmigo?";
	next;
	mes "[Yotang]";
	mes "Necesito ^0000FF100 Transparent Celestial Robes, 100 Soft Silk Cloth,";
	mes "1 Wedding Bouquet, 1 Violet Dyestuffs";
	mes "y ^0000FF1 Bandana^000000 que peudes comprar en la tienda,";
	mes "para hacerla ^0000FFFlower of Fairy^000000.";
	close;

L_CheckWeight:
	if (MaxWeight - Weight < 200) {
		mes "[Yotang]";
		mes "Tienes tu mochila llena, amigo.";
		mes "Vuelve despues de";
		mes "limpiar el inventario.";
		close;
	}
	if (checkweight(5437,1) == 0) {
		mes "- Espera !! -";
		mes "- Tienes muchos items! -";
		mes "- Te hes imposible tener mas. -";
		mes "- Ven denuevo -";
		mes "- despues de vaciarla. -";
		close;
	}
	return;
}

lhz_in02,276,281,1	script	Drunken Man#Necktie	748,{
	mes "[Drunken Man]";
	mes "Hey... hip!, buscas pelea?!";
	next;
	mes "El se ve bastante borracho, que haras?";
	next;
	if(select("Ignorarlo.:Pelear!") == 1) {
		mes "[Drunken Man]";
		mes "hip!, por que me ignoras?";
		mes "Me tienes miedo muchachito?";
		close;
	}
	if (BaseLevel < 65) {
		mes "[Drunken Man]";
		mes "Que... eres un debilucho.";
		mes "Mejor ve y sube de niveles!";
		emotion ET_GO;
		close;
	}
	mes "[Drunken Man]";
	mes "Espera! Necesito alguna armadura hip!, que usar.";
	mes "Esta!, Necktie, Necktie...";
	next;
	mes "[Drunken Man]";
	mes "Una vez que uso la <ITEM>Necktie<INFO>5442</INFO></ITEM> en mi cabeza,";
	mes "Te derrotare facil!";
	mes "Debes estar nervioso..";
	mes "Hmm! hip!, Donde esta mi Necktie?";
	next;
	if (countitem(978) > 0 && countitem(7151) > 29 && countitem(1059) > 99 && countitem(7217) > 19) {
		mes "[Drunken Man]";
		mes "Oh, tienes los materiales que necesita la Necktie! Damelos, hip!!";
		next;
		if(select("Olvidalo!:Ten buen hombre.") == 1) {
			mes "[Drunken Man]";
			mes "Ohh, tienes mieeedo!";
			mes "Gane denuevo!!";
			close;
		}
		callsub L_CheckWeight;
		mes "[Drunken Man]";
		mes "Bien! espera un momento!";
		next;
		mes "Esto aqui, esto alla... ch...ch... hip...";
		emotion ET_SWEAT;
		next;
		mes "[Drunken Man]";
		mes "-Bebe...bebe....- Zzzzz...";
		emotion ET_QUESTION, playerattached();
		next;
		if (countitem(978) > 0 && countitem(7151) > 29 && countitem(1059) > 99 && countitem(7217) > 19) {
			callsub L_CheckWeight;
			mes "Callo en la barra a dormir!";
			mes "Almenos lo termino.";
			mes "Ten, toma tu Necktie.";
			delitem 978,1; //Cobaltblue_Dyestuffs
			delitem 7151,30; //Oil_Paper
			delitem 1059,100; //Transparent_Cloth
			delitem 7217,20; //Spool
			getitem 5442,1; //Necktie
			close;
		}
		mes "[Drunken Man]";
		mes "Huh? Donde se fueron mis materiales?";
		mes "Te los llevaste?..hip! Tanto miedo me tienes?";
		close;
	}
	mes "[Drunken Man]";
	mes "Hey tú, ve y consigueme";
	mes "^FF00001 Cobaltblue Dyestuff, 30 Oil Papers, 100 Fabrics, 20 Spools^000000!";
	next;
	mes "[Drunken Man]";
	mes "Te derrotare usando la legendaria <ITEM>Necktie<INFO>5442</INFO></ITEM>!";
	next;
	if(select("No quiero.:Vamos a buscar los items.") == 1) {
		mes "[Drunken Man]";
		mes "Huyes..hip?";
		mes "Gane otra vez?";
		close;
	}
	mes "[Drunken Man]";
	mes "Te lo dire una vez mas,";
	mes "por si lo olvidaste, cabeza dura!";
	next;
	mes "[Drunken Man]";
	mes "^FF00001 Cobaltblue Dyestuff, 30 Oil Papers, 100 Fabrics, 20 Spools^000000!";
	mes "Deprisa!! quiero pelea!hip!.";
	emotion ET_GO;
	close;

L_CheckWeight:
	if (MaxWeight - Weight < 100) {
		mes "Necktie es muy pesada para ti.";
		mes "No la podrias tener ni en sueños.";
		close;
	}
	if (checkweight(5442,1) == 0) {
		mes "- Espera !! -";
		mes "- Tienes muchos items, por eso -";
		mes "- te hes imposible tener mas. -";
		mes "- Ven denuevo y asegurate -";
		mes "- de vaciarte un poco!. -";
		close;
	}
	return;
}

comodo,236,164,0	script	Hat Girl#Hair_Brush	69,{
	mes "[Hat Girl]";
	mes "No es mucho mejor tener un Hat Único?";
	mes "Mas que esas comunes Orejas de Gato?";
	next;
	if(select("Prefiero las orejitas!:Hat únicos son mejor!") == 1) {
		mes "[Hat Girl]";
		mes "Si? pues usa tu aburridas Orejas como todos los demás!";
		emotion ET_HNG;
		close;
	}
	mes "[Hat Girl]";
	mes "Tienes buen sentido de la Moda!";
	mes "Verás, te puedo hacer un";
	mes "Hat Fabuloso!.";
	next;
	mes "[Hat Girl]";
	mes "Te daré el honor,";
	mes "de parecer una Super estrella,";
	mes "de la Moda! si me traes los siguientes materiales..";
	next;
	if (countitem(974) > 0 && countitem(975) > 0 && countitem(943) > 49 && countitem(985) > 4 && countitem(1302) > 0) {
		mes "[Hat Girl]";
		mes "Oh! tienes los ^0000FFmateriales para un^000000 <ITEM>Hair Brush<INFO>5444</INFO></ITEM>.";
		mes "Quieres crearlo ahora mismo?";
		next;
		if(select("Creo que hay hat mejores...:Si! crealo por mi!") == 1) {
			mes "[Hat Girl]";
			mes "Si? pues sigue llevando tus Hats aburridos.";
			emotion ET_HNG;
			close;
		}
		callsub L_CheckWeight;
		mes "[Hat Girl]";
		mes "Okay, lo haré enseguida!";
		next;
		mes "Ch ch ch... combinando...ch ch.";
		emotion ET_PROFUSELY_SWEAT, playerattached();
		next;
		mes "[Hat Girl]";
		mes "Listo! Terminado!";
		mes "Ahora eres la nueva Fashionista!";
		next;
		if (countitem(974) > 0 && countitem(975) > 0 && countitem(943) > 49 && countitem(985) > 4 && countitem(1302) > 0) {
			callsub L_CheckWeight;
			mes "[" + strcharinfo(0) + "]";
			mes "Ten tu nuevo Hair Brush!.";
			mes "...aunque me pone triste.";
			delitem 974,1; //Mixture
			delitem 975,1; //Scarlet_Dyestuffs
			delitem 943,50; //Solid_Shell
			delitem 985,5; //Elunium
			delitem 1302,1; //Axe_
			getitem 5444,1; //Hair_Brush
			close;
		}
		mes "[Hat Girl]";
		mes "Huh? No quieres ser";
		mes "el más fashionista?";
		mes "No tienes suficientes materiales?";
		close;
	}
	mes "[Hat Girl]";
	mes "Para hacer la última moda...";
	mes "el <ITEM>Hair Brush<INFO>5444</INFO></ITEM>";
	mes "Necesito mezclar ^0000FF1 Mixture, 1 Scarlet Dyestuffs,";
	mes "50 Solid Shells, 5 Eluniums,^000000";
	mes "y 1 ^0000FFAxe^000000 con 4 slots en él.";
	next;
	mes "[Hat Girl]";
	mes "Que haces aún allí? Apurate! la Moda no espera!";
	next;
	mes "[Hat Girl]";
	mes "Tengo un Tip para tí.";
	mes "Puedes encontrar fácilmente Solid Shells y Elunium de los Monstruos! usa el comando @whodrops.";
	close;

L_CheckWeight:
	if (MaxWeight - Weight < 100) {
		mes "[Hat Girl]";
		mes "Llevas muchas porquerías!.";
		mes "Vuelve cuando hayas vaciado tu inventario.";
		close;
	}
	if (checkweight(5443,1) == 0) {
		mes "- Espera !! -";
		mes "- Tienes demasiados items!, asi que -";
		mes "- te hes imposible llevar más. -";
		mes "- Vuelve luego cuando -";
		mes "- hayas vaciado un poco más. -";
		close;
	}
	return;
}

alberta,151,79,3	script	Bebete#Baby_Angel	96,{
	if (isequipped(5074) > 0 || isequipped(2254) > 0 || isequipped(5210) > 0) {
		if (BaseLevel < 65) {
			mes "[Bebete]";
			mes "Un ángel falso? Vete!";
			next;
			mes "[Bebete]";
			mes "Un ángel real..";
			mes "no tiene el nivel tán bajo como tú.";
			close;
		}
		if (isequipped(5443) > 0) {
			mes "[Bebete]";
			mes "Oh, mi ángel volvió.";
			mes "Estás satisfecho con la ^FF0000Statue Of Baby Angel^000000, a que sí?";
			close;
		}
		if (countitem(523) > 0 && countitem(916) > 499 && countitem(7292) > 9 && countitem(2282) > 0 && countitem(640) > 0) {
			mes "[Bebete]";
			mes "Ah! Oye, ángel~ ángel~";
			next;
			mes "[Bebete]";
			mes "Por favor muestrame una";
			mes "<ITEM>Statue of Baby Angel<INFO>5443</INFO></ITEM>";
			mes "He oido que todo ángel tiene uno!";
			next;
			select("Statue of Baby Angel?");
			mes "[Bebete]";
			mes "Lo has perdido?";
			mes "Quieres que haga otra para tí?";
			next;
			if(select("No, gracias.:Si! quiero una.") == 1) {
				mes "[Bebete]";
				mes "Oh... no es tan fácil como suena!";
				close;
			}
			callsub L_CheckWeight;
			mes "[Bebete]";
			mes "Woo! andas trayendo todos los items.";
			emotion ET_BIGTHROB;
			next;
			mes "[Bebete]";
			mes "Estas de suerte, justo terminaba de hacer una,";
			mes "Así que no deberás esperar tanto.";
			next;
			if (countitem(523) > 0 && countitem(916) > 499 && countitem(7292) > 9 && countitem(2282) > 0 && countitem(640) > 0) {
				callsub L_CheckWeight;
				mes "Tengo la ^FF0000Statue of Baby Angel^000000.";
				delitem 916,500; //Feather_Of_Birds
				delitem 523,1; //Holy_Water
				delitem 7292,10; //Muscovite
				delitem 2282,1; //Spirit_Chain
				delitem 640,1; //Shining_Stone
				getitem 5443,1; //Status_Of_Baby_Angel
				next;
				mes "[Bebete]";
				mes "Nos vemos luego, mi ángel!~!";
				emotion ET_THANKS;
				next;
				mes "[" + strcharinfo(0) + "]";
				mes "... ...";
				next;
				mes "[" + strcharinfo(0) + "]";
				mes "Hmm... creo que esa pequeña me confundió..";
				close;
			}
			mes "[Bebete]";
			mes "Oyee! no veo ningún";
			mes "material en tu inventario..";
			emotion ET_CRY;
			close;
		}
		mes "[Bebete]";
		mes "Hey, ángel...";
		next;
		mes "[Bebete]";
		mes "Wow, un ángel!";
		emotion ET_HUK;
		next;
		mes "[Bebete]";
		mes "Eres un ángel real? Vienes desde el cielo?";
		next;
		if(select("Que tontera!:Mantengamos la inocencia de la niña!") == 1) {
			mes "[" + strcharinfo(0) + "]";
			mes "Ángeles y el Cielo? Claro que sí! ";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "JAJAJA, lo creías? todo eso son mentiras!";
			emotion ET_KIK, playerattached();
			next;
			mes "[Bebete]";
			mes "Eres un demonio, un monstruo, un idiota!";
			emotion ET_OHNO;
			close;
		}
		mes "[" + strcharinfo(0) + "]";
		mes "Solía ir a bailes de salón con Dragones y Zebras multicolor";
		mes "en nubes con sabor a Vainilla!.";
		next;
		mes "[Bebete]";
		mes "Es justo lo que siempre he imaginado!";
		next;
		mes "[Bebete]";
		mes "Por favoor! muestrame tu";
		mes "<ITEM>Statue of Baby Angel<INFO>5443</INFO></ITEM>!";
		mes "Escuche que todo ángel lleva una!";
		next;
		mes "[Bebete]";
		mes "Ow.. la perdiste?";
		mes "Quieres que haga una para tí?";
		next;
		mes "[Bebete]";
		mes "Ya he echo muchas antes,";
		mes "para otros ángeles como tú.";
		next;
		mes "[Bebete]";
		mes "Tan sólo necesito ^FF0000500 Feather Of Birds, 1 Holy Water, 10 Muscovite, 1 Heaven Ring y 1 Shining Stone^000000.";
		mes "Recuerda usar el comando @whodrops!";
		next;
		mes "[Bebete]";
		mes "Yap! Córrele!";
		close;
	} else {
		mes "[Bebete]";
		mes "Un ángel falso? Vete!!";
		next;
		mes "[Bebete]";
		mes "Un ángel de verdad,";
		mes "una alas en su cabeza!";
		close;
	}
	end;

L_CheckWeight:
	if (MaxWeight - Weight < 600) {
		mes "[Bebete]";
		mes "Por que llevas tantas cosas, mi ángel?";
		mes "No puedo entregarte el item..";
		mes "Porque tu mochila está llena!.";
		close;
	}
	if (checkweight(5443,1) == 0) {
		mes "- Espera !! -";
		mes "- Tienes muchos items encima, así -";
		mes "- que te hes imposible llevar más. -";
		mes "- vuelve denuevo, -";
		mes "- después de vaciarla un poco. -";
		close;
	}
	return;
}

gef_fild07,108,161,4	script	Roda Frog#Frog_Cap	1012,1,1,{
	if (!.Active) end;
	if (hat_2009 == 0) {
		mes "[???]";
		mes "Hmm..Hm..";
		mes "Parece que mi transformación en apariencia de niño fue exitosa.";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "... ... ... ... ah?";
		next;
		mes "[???]";
		mes "Humano! Estoy aquí porque necesito pedirte ayuda.";
		next;
		if (BaseLevel < 65) {
			mes "[???]";
			mes "... ... ...";
			next;
			mes "[???]";
			mes "Soy el gran Rey Froggie VII!";
			next;
			mes "[King Froggie VII]";
			mes "pero al parecer no eres lo suficientemente fuerte para ayudarme en esto.";
			mes "Ve y entrena aún más!";
			next;
			mes "[King Froggie VII]";
			mes "Pasando el Nivel 65 de Humano estará bien.";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		} else {
			mes "[???]";
			mes "Soy el gran rey, Froggie VII..";
			mes "Y necesito tu ayuda!";
			next;
			if(select("Ignorarlo..:Vamos a ayudarlo!.") == 1) {
				mes "[" + strcharinfo(0) + "]";
				mes "Una rana hablando...";
				next;
				mes "[" + strcharinfo(0) + "]";
				mes "Ya lo he visto todo.";
				next;
				mes "[King Froggie VII]";
				mes "Croaking...Croaking...";
				close2;
				donpcevent "Roda Frog#Frog_Cap::OnDisable";
				end;
			}
			mes "[" + strcharinfo(0) + "]";
			mes "En que puedo ayudar?";
			next;
			mes "[King Froggie VII]";
			mes "Nuestra gran raza de ranas está en peligro porque muchos viajeros vinieron aquí y se llevaron nuestros huevos como una especie de souvenirs. Incluso escuché que quedan muchos aquí y allá, así que necesito tu ayuda. Por favor traer de vuelta ^FF0000100 Spawn^000000.";
			setquest 5000;
			set hat_2009,1;
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Claro, lo haré.";
			next;
			mes "[King Froggie VII]";
			mes "Estaré esperandote!!";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		}
	} else if (hat_2009 == 1) {
		if (countitem(908) < 100) {
			mes "[King Froggie VII]";
			mes "Que haces aún aqui?";
			mes "Ve y consigueme ^FF0000100 Spawn^000000! recuerda usar el comando @whodrops!";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		} else {
			mes "[King Froggie VII]";
			mes "Ah! las has traido!";
			next;
			mes "[King Froggie VII]";
			mes "Muchísimas gracias! " + strcharinfo(0) + ".";
			emotion ET_THANKS;
			next;
			mes "[King Froggie VII]";
			mes "Aunque.. tenemos otro asunto urgente. Así que necesitare un poco más de ayuda.";
			next;
			mes "[King Froggie VII]";
			mes "Verás, las ranas son comida para las Serpientes!.";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Lo sé bien..";
			next;
			mes "[King Froggie VII]";
			mes "Bueno.. no sientes pena por la raza de las ranas?";
			next;
			mes "[King Froggie VII]";
			mes "Pues, quería pedirte si puedes vengarlas.. Mata a ^FF000050 Side Winders^000000 por nosotros.";
			mes "Puedes usar el comando @whereis para saber donde aparecen estas malvadas serpientes!";
			delitem 908,100; //Spawn
			changequest 5000,5001;
			set hat_2009,2;
			next;
			mes "[King Froggie VII]";
			mes "Esperare noticias nuevas!";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		}
	} else if (hat_2009 == 2) {
		set .@hunting, checkquest(5001,HUNTING);
		if (.@hunting == 0 || .@hunting == 1) {
			mes "[King Froggie VII]";
			mes "He estado esperando por tus buenas noticias.";
			mes "Debe ser pan comido para ti!";
			mes "matar a esas ^FF000050 Side Winders^000000. Sigue así!";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		} else if (.@hunting == 2) {
			mes "[" + strcharinfo(0) + "]";
			mes "Volví.";
			next;
			mes "[King Froggie VII]";
			mes "Al parecer has matado a todas las Side Winders!";
			next;
			mes "[King Froggie VII]";
			mes "Te convertirás en un ejemplo para las jovenes ranas. Gracias otra vez.";
			emotion ET_THANKS;
			next;
			mes "[King Froggie VII]";
			mes "Por lo tanto, estoy considerando agregar su sobresaliente masacre al libro de historia de nuestra rana. Pero necesito evidencia para este trabajo.";
			next;
			mes "[King Froggie VII]";
			mes "Por favor traeme ^FF0000 100 ^000000 ^FF0000Venom Canines^000000 de esas serpientes.";
			changequest 5001,5002;
			set hat_2009,3;
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Estaré orgulloso de estar en sus libros de Historia, iré por ellas.";
			next;
			mes "[King Froggie VII]";
			mes "Obediente, trabajador... Quisiera ser una rana tan leal como tú.";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		}
	} else if (hat_2009 == 3) {
		if (countitem(937) < 100) {
			mes "[King Froggie VII]";
			mes "Como van esas ^FF0000100 Venom Canines^000000?";
			mes "Deprisa, es una rara oportunidad para entrar al libro de Historia de Ranas!";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		} else {
			mes "[King Froggie VII]";
			mes "Ah! conseguiste esas Venom Canines! Ahora oficialmente entrarás al libro de Historia de las Ranas!";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Está bien para mi, es un honor.";
			next;
			mes "[King Froggie VII]";
			mes "Y tengo mejores noticias!";
			next;
			mes "[King Froggie VII]";
			mes "Encontré una mejor manera de mantener a nuestra raza viva, mientras esperamos el libro!";
			next;
			mes "[King Froggie VII]";
			mes "La respuesta está justamente en el Libro de Historia, hace años atrás, el gran Rey Froggie III creó una medicina secreta que nos permitiría usar habilidades para escondernos de las serpientes.";
			next;
			mes "[King Froggie VII]";
			mes "Si tan solo lo hubiera sabido antes...";
			mes "Por favor traeme ^FF000050 Scaled Shells^000000.";
			delitem 937,100; //Posionous_Canine
			changequest 5002,5003;
			set hat_2009,4;
			next;
			mes "[King Froggie VII]";
			mes "No son faciles de conseguir pero confío en ti.";
			mes "Usa el comando @whodrops para saber quien las deja caer!";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		}
	} else if (hat_2009 == 4) {
		if (countitem(936) < 50) {
			mes "[King Froggie VII]";
			mes "Todavía aqui? ve y consigue ^FF000050 Scaled Shells^000000! Por favor, deprisa!";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		} else {
			mes "[King Froggie VII]";
			mes "Excelente!!";
			mes "Eres como el mismisimo Rey Froggie III en sus dias jovenes!";
			next;
			mes "[King Froggie VII]";
			mes "Ahora eres el héroe de las Ranas! Si tengo una hija, debes casarte con ella!";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Ah... No, gracias..";
			mes "^FF0000(Enserio me dices que me case con una rana???)^000000";
			next;
			mes "[King Froggie VII]";
			mes "Gracias a ti, la población de ranas ha aumentado, pero nos faltan alimentos.";
			next;
			mes "[King Froggie VII]";
			mes "La mejor comida para nosotros son los ^FF0000Argiope^000000.";
			next;
			mes "[King Froggie VII]";
			mes "Entonces que te parece, héroe?";
			mes "Ve y traenos ^FF000050^000000 deliciosos^FF0000 Argiopes^000000!";
			delitem 936,50; //Scales_Shell
			set hat_2009,5;
			changequest 5003,5004;
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		}
	} else if (hat_2009 == 5) {
		set .@hunting, checkquest(5004,HUNTING);
		if (.@hunting == 0 || .@hunting == 1) {
			mes "[King Froggie VII]";
			mes "Mi héroe, quieres matarnos de hambre?";
			mes "Tráenos esos ^FF000050 Argiopes^000000!";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		} else if (.@hunting == 2) {
			mes "[King Froggie VII]";
			mes "Creo que ya te lo había dicho, pero te has convertido en el héroe de las ranas...";
			next;
			mes "[King Froggie VII]";
			mes "Tuve mucha suerte al pedirte ese favor.";
			next;
			if (MaxWeight - Weight < 500) {
				mes "[King Froggie VII]";
				mes "Quiero darte una recompensa, mi héroe. Pero tu mochila está muy llena.";
				close2;
				donpcevent "Roda Frog#Frog_Cap::OnDisable";
				end;
			}
			if (checkweight(5447,1) == 0) {
				mes "- Espera !! -";
				mes "- Tienes demasiados items, así que -";
				mes "- te hes imposible tener más. -";
				mes "- Vuelve denuevo -";
				mes "- después de haberla vaciado un poco. -";
				close;
			}
			mes "[King Froggie VII]";
			mes "Ranas, tienen algúna objeción";
			mes "para entregarle a nuestro héroe, " + strcharinfo(0) + "";
			mes "un <ITEM>Frog Hat<INFO>5447</INFO></ITEM> el cual es uno de nuestros preciados tesoros?";
			next;
			mes "[Sonidos desde Lejos]";
			mes "Croak...croak...";
			mes "croooooaaaaak~~~";
			mes "Croak croak";
			next;
			mes "[King Froggie VII]";
			mes "Hmm, no hay objeciones.";
			next;
			mes "[King Froggie VII]";
			mes "Me complace concederle a " + strcharinfo(0) + ", nuestro héroe, este Frog Hat.";
			specialeffect2 EF_MAPPILLAR2;
			specialeffect2 EF_BLESSING;
			erasequest 5004;
			set hat_2009,6;
			getitem 5447,1; //Frog_Cap
			next;
			mes "[King Froggie VII]";
			mes "Siempre serás binvenido.";
			mes "Visitanos cuando quieras!.";
			close2;
			donpcevent "Roda Frog#Frog_Cap::OnDisable";
			end;
		}
	} else if (hat_2009 == 6) {
		mes "[King Froggie VII]";
		mes "Todas las ranas de la tierra, alaben a " + strcharinfo(0) + ", nuestro eterno héroe!";
		emotion ET_THANKS;
		close2;
		donpcevent "Roda Frog#Frog_Cap::OnDisable";
		end;
	}
	end;
//OnInit:
//	setnpcdisplay "Roda Frog#Frog_Cap",1012; //RODA_FROG
//	end;
OnEnable:
	setnpcdisplay "Roda Frog#Frog_Cap",716; //4_M_KID2
	set .Active,1;
	end;
OnDisable:
	set .Active,0;
	disablenpc "Roda Frog#Frog_Cap";
	initnpctimer;
	end;
OnReset:
	setnpcdisplay "Roda Frog#Frog_Cap",1012; //RODA_FROG
	enablenpc "Roda Frog#Frog_Cap";
	end;
OnTimer2000:
	donpcevent "Roda Frog#Frog_Cap::OnReset";
	stopnpctimer;
	end;
OnTouch:
	if (.Active) end;
	mes "Mira, una rana.. Que harás?";
	next;
	switch(select("Lanzarle una piedra.:Conversarle.")) {
	case 1:
		donpcevent "Roda Frog#Frog_Cap::OnDisable";
		mes "Ouch! ~";
		close;
	case 2:
		donpcevent "Roda Frog#Frog_Cap::OnEnable";
		if (hat_2009 > 0) {
			emotion ET_THINK, playerattached();
			mes "[" + strcharinfo(0) + "]";
			mes "Siempre es increíble de ver";
			mes "una rana transformandose en un niño.";
			close;
		} else {
			emotion ET_HUK, playerattached();
			mes "[" + strcharinfo(0) + "]";
			mes "La.. la rana se transformó en un niño..";
			close;
		}
	}
	end;
}

alberta,120,206,3	script	Alonie#Solo_Play_Box	787,{
	if (hat_1_2009 == 0) {
		mes "[Alonie]";
		mes "I am an expert at playing alone.";
		mes "I'm trying my best to play alone!";
		next;
		mes "[Alonie]";
		mes "HuHu, Yapyap!";
		mes "I can feel your admiration for me.";
		mes "Wanna be my student?";
		next;
		switch(select("You're out of your mind!:Please let me be your disciple!")) {
		case 1:
			mes "[Alonie]";
			mes "Yes, I'm out of my mind.";
			mes "Huhu, Yapyap!";
			close;
		case 2:
			mes "[Alonie]";
			mes "I can feel you are lack of affection. Okay!";
			mes "Now, let's start the training!";
			next;
			mes "[Alonie]";
			mes "The special training, to play alone in Sphinx Dungeon!";
			mes "For make it";
			mes "the fruitful training,";
			mes "Get around and kill";
			mes "^FF000020 Zeroms, 20 Requiems^000000!";
			mes "Hurry!";
			next;
			switch(select("What bullshit!:Let's go!")) {
			case 1:
				mes "[Alonie]";
				mes "Of course, I don't care.";
				mes "Huhu, Yapyap!";
				close;
			case 2:
				mes "[Alonie]";
				mes "Hurry up, my student.";
				mes "I am so capricious";
				mes "so I don't know what I will";
				mes "give you as a reward!";
				setquest 1100;
				setquest 1101;
				set hat_1_2009,1;
				close;
			}
		}
	} else if (hat_1_2009 == 1) {
		if (checkquest(1100,HUNTING) == 2 && checkquest(1101,HUNTING) == 2) {
			mes "[Alonie]";
			mes "Already finished?";
			mes "Good, here is the next training";
			mes "to be a playing-alone expert!";
			next;
			mes "[Alonie]";
			mes "The special training, to play alone at Clock Tower!";
			mes "For make it";
			mes "the fruitful training,";
			mes "Get around and kill";
			mes "^FF000040 Arclouses and 30 High Orcs^000000!";
			next;
			switch(select("What Bullshit!:Let's go!")) {
			case 1:
				mes "[Alonie]";
				mes "Of course, I don't care.";
				mes "Huhu, Yapyap!";
				close;
			case 2:
				mes "[Alonie]";
				mes "Hurry up, my student.";
				mes "I am so capricious";
				mes "so I don't know what I will";
				mes "give you as a reward!";
				changequest 1100,1102;
				changequest 1101,1103;
				set hat_1_2009,2;
				close;
			}
		} else {
			mes "[Alonie]";
			mes "Hmm, it's not enough.";
			mes "Playing alone is not that easy!";
			mes "Go and kill";
			mes "^FF000020 Zeroms, 20 Requiems^000000!";
			close;
		}
	} else if (hat_1_2009 == 2) {
		if (checkquest(1102,HUNTING) == 2 && checkquest(1103,HUNTING) == 2) {
			mes "[Alonie]";
			mes "You're really good at playing alone...!";
			mes "Are you lonely?";
			mes "Well, here is the next training";
			mes "to be a playing-alone expert!";
			next;
			mes "[Alonie]";
			mes "The special training, to play alone in Louyang!";
			mes "For make it";
			mes "the fruitful training,";
			mes "Get around and kill";
			mes "^FF000020 Green Maidens and 50 Mao Guais^000000!";
			next;
			switch(select("What Bullshit!:Let's go!")) {
			case 1:
				mes "[Alonie]";
				mes "Of course, I don't care.";
				mes "Huhu, Yapyap!";
				close;
			case 2:
				mes "[Alonie]";
				mes "Hurry up, my student.";
				mes "I am so capricious";
				mes "so I don't know what I will";
				mes "give you as a reward!";
				changequest 1102,1108;
				changequest 1103,1105;
				set hat_1_2009,3;
				close;
			}
		} else {
			mes "[Alonie]";
			mes "Hmm, It's not enough.";
			mes "Playing alone is not that easy!";
			mes "Go and kill";
			mes "^FF000040 Arclouses and 30 High Orcs^000000!";
			close;
		}
	} else if (hat_1_2009 == 3) {
		if (checkquest(1108,HUNTING) == 2 && checkquest(1105,HUNTING) == 2) {
			mes "[Alonie]";
			mes "Great job.";
			mes "How lonely you are!";
			mes "Okay, here is the last training";
			mes "to be a real playing-alone expert!";
			next;
			mes "[Alonie]";
			mes "The special training, to play alone in Amatsu Dungeon!";
			mes "For make it";
			mes "the fruitful training,";
			mes "Get around and kill";
			mes "^FF000020 The Papers and 40 Firelock Soldiers^000000!";
			next;
			switch(select("What Bullshit!:Let's go!")) {
			case 1:
				mes "[Alonie]";
				mes "Of course, I don't care.";
				mes "huhu, Yapyap!";
				close;
			case 2:
				mes "[Alonie]";
				mes "Hurry up, my student!";
				mes "I am so capricious";
				mes "so I don't know what I will";
				mes "give you as a reward!";
				changequest 1108,1106;
				changequest 1105,1107;
				set hat_1_2009,4;
				close;
			}
		} else if (checkquest(1104,HUNTING) == 2 && checkquest(1105,HUNTING) == 2) {
			mes "[Alonie]";
			mes "Great job.";
			mes "How lonely you are!";
			mes "Okay, here is the last training";
			mes "to be a real playing-alone expert!";
			next;
			mes "[Alonie]";
			mes "The special training, to play alone in Amatsu Dungeon!";
			mes "For make it";
			mes "the fruitful training,";
			mes "Get around and kill";
			mes "^FF000020 The Papers and 40 Firelock Soldiers^000000!";
			next;
			switch(select("What Bullshit!:Let's go!")) {
			case 1:
				mes "[Alonie]";
				mes "Of course, I don't care.";
				mes "huhu, Yapyap!";
				close;
			case 2:
				mes "[Alonie]";
				mes "Hurry up, my student!";
				mes "I am so capricious";
				mes "so I don't know what I will";
				mes "give you as a reward!";
				changequest 1104,1106;
				changequest 1105,1107;
				set hat_1_2009,4;
				close;
			}
		} else {
			mes "[Alonie]";
			mes "Hmm, It's not enough.";
			mes "Playing alone is not that easy!";
			mes "Go and kill";
			mes "^FF000020 Green Maidens and 50 Mao Guais^000000!";
			if (checkquest(1104,HUNTING) == 1) {
				erasequest 1104;
				setquest 1108;
			}
			close;
		}
	} else if (hat_1_2009 == 4) {
		if (MaxWeight - Weight < 300) {
			mes "[Alonie]";
			mes "Playing outside with this heavy bag?";
			mes "You are the real";
			mes "expert to play alone!";
			next;
			mes "[Alonie]";
			mes "If you want the reward, empty your bag, first!";
			close;
		}
		if (checkquest(1106,HUNTING) == 2 && checkquest(1107,HUNTING) == 2) {
			mes "[Alonie]";
			mes "Finally you did it.";
			mes "My affection-lacked student!";
			mes "This is the end of your training!";
			next;
			if (gettime(DT_HOUR) < 12) {
				set .@item,5448; //Solo_Play_Box1
				set .@time$,"AM";
			} else {
				set .@item,5449; //Solo_Play_Box2
				set .@time$,"PM";
			}
			if (checkweight(.@item,1) == 0) {
				mes "[Alonie]";
				mes "Playing outside with this heavy bag?";
				mes "You are the real";
				mes "expert to play alone!";
				next;
				mes "[Alonie]";
				mes "If you want the reward, empty your bag, first!";
				close;
			}
			mes "[Alonie]";
			mes "Here! The solo play box";
			mes "the most effective at " + .@time$ + "!";
			mes "After putting on this box,";
			mes "you're the best lonely being!";
			next;
			mes "[Alonie]";
			emotion ET_BEST;
			mes "Fight back your tears!";
			mes "Huhu, Yapyap!";
			completequest 1106;
			completequest 1107;
			set hat_1_2009,5;
			getitem .@item,1;
			close;
		} else {
			mes "[Alonie]";
			mes "Hmmm, It's not enough.";
			mes "Playing alone is not that easy thing!";
			mes "Go and kill";
			mes "^FF000020 The Papers and 40 Firelock Soldiers^000000!";
			close;
		}
	} else if (hat_1_2009 == 5) {
		mes "[Alonie]";
		mes "I am the playing-alone expert.";
		mes "Always trying my best to play alone!";
		next;
		mes "[Alonie]";
		mes "Huhu, Yapyap!";
		close;
	}
	end;
}
