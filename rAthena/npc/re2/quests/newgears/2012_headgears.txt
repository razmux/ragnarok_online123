//===== rAthena Script =======================================
//= 2012 Headgear Quests
//===== By: ==================================================
//= Euphy, -SkittleNugget-
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Enhance gears by synthesizing them with Energy Crystals.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//=     Script is a little messy, could use some cleaning.
//============================================================

// Main NPC :: 2012_hat_quest
//============================================================
moc_para01,41,169,3	script	Reno#2012hat	64,{

	// iRO has (very) minor differences in dialogue from the original script.
	// To use iRO's version, uncomment the line below.
	//set .@features_iRO,1;

	//custom translation -  all checks below
	if (checkweight(1301,1) == 0 || MaxWeight - Weight < 800) {
		mes "- Llevas muchos items!, no puedo proceder. -";
		close;
	}

	setarray .@type$[0],"lower","intermediate","advanced","highest";
	setarray .@crystal$[0],"Rough","Purified","High";

	// playtime checks
	setarray .@quests_playtime[0],5161,5169,5174,5225,5226,5227;
	for (set .@i,0; .@i<6; set .@i,.@i+1) {
		if (checkquest(.@quests_playtime[.@i],PLAYTIME) == 2) {
			erasequest .@quests_playtime[.@i];
			mes "[Reno]";
			if (.@i < 3)	// hunting quest wait time
				mes "El tiempo de espera para la recolección de "+.@type$[.@i]+" expiró. Ahora puedes tomar otra cazería.";
			else        	// buff wait time
				mes "El tiempo de espera para cobrar las "+.@crystal$[.@i]+" Energy Crystals expiró. Ahora puedes cobrarlas denuevo.";
			close;
		}
	}

	// hunting quest checks
	callsub L_CheckHunting,5161,5162,7,.@type$[0],6623,20; //Rough_Energy_Crystal
	callsub L_CheckHunting,5169,5170,4,.@type$[1],6624,20; //Purified_Energy_Crystal
	callsub L_CheckHunting,5174,5175,4,.@type$[2],6625,10;  //High_Purity_Energy_Xtal
	callsub L_CheckHunting,5174,5179,4,.@type$[3],6625,20; //High_Purity_Energy_Xtal

	// buff checks
	set .@energy_buf00, getstatus(SC_QUEST_BUFF1) + getstatus(SC_QUEST_BUFF2) + getstatus(SC_QUEST_BUFF3);
	if (!.@energy_buf00) {
		for (set .@i,0; .@i<3; set .@i,.@i+1) {
			if (isbegin_quest(.@i + 5222)) {
				set .@item, 6623 + .@i;
				set .@amount, 2 + rand(3);
				mes "[Reno]";
				mes getitemname(.@item)+" storage is now complete.";
				next;
				setquest 5225 + .@i;
				erasequest 5222 + .@i;
				getitem .@item,.@amount;
				mes "[Reno]";
				mes "^0000FF"+.@amount+" "+getitemname(.@item)+"s^000000 were extracted.";
				close;
			}
		}
	}

	mes "[Reno]";
	mes "Hola? Mi nombre es ^0000FFReno^000000, trabajo para la Energy Crystal Research Association, o ECRA.";
	mes "Trabajo con los llamados ^0000FFEnergy Crystals^000000 o ^0000FFEC^000000 con los que me hes posible darte poderosos Buffs o Hats, también puedo entregarte ^0000FFEC^000000 a cambio de cazerías de mobs.";
	next;
	switch(select("[ ECRA? ]:[ Cazerías por ^0000FFEC^000000 ]:[ Buff por ^0000FFEC^000000 ]:[ Comprimir ^0000FFCristales^000000 ]:[ Cambio ^0000FFEC^000000 por Hats ]:[ Hat al azar por ^0000FFEC^000000 ]" + ((.@features_iRO)?":See Equipment List":""))) {
	case 1:
		mes "[Reno]";
		mes "ECRA es una institución que investiga unos cristales llamados '^0000FFEnergy Crystals^000000' o ^0000FFECs ^000000que se utilizan como una fuente de energía alternativa en el Reino de Rune Midgart, que sufre de una escasez de energía.";
		next;
		select("Que razón tienen para hacer esto?");
		mes "[Reno]";
		mes "Me gustaría explicarte que no estamos tratando de hacer una excusa para evitar enfrentar un problema antes de explicar el motivo. ¡Nunca! Así que por favor déjame terminar.";
		next;
		mes "[Reno]";
		mes "El equipo de investigación me envió los resultados de su arduo trabajo.";
		next;
		mes "[Reno]";
		mes "Los cristales de energía resultaron ser ^0000FFgenerados dentro de los cuerpos de algunos monstruos^000000, y según la investigación, los monstruos más fuertes generan cristales de energía de clase superior en sus cuerpos.";
		next;
		mes "[Reno]";
		mes "Nuestro Equipo decidió recolectar estos cristales dando solicitudes a los aventureros, con recompensas apropiadas. Eso nos permite recolectar mejor y ser más ^FF0000eficientes^000000.";
		next;
		mes "[Reno]";
		mes "Los cristales, acumulados a través de la caza de monstruos a petición mía, se utilizarán para mi investigación. ^0000FFLos cuales algunos serán devueltos a los aventureros.^000000";
		next;
		mes "[Reno]";
		mes "Entonces, los aventureros podrán usar estos cristales para ^FF0000subir de nivel el equipo^000000 a travéz de mi.";
		next;
		mes "[Reno]";
		mes "Si resulta todo bien, será beneficioso para ECRA y para ustedes, aventureros.";
		close;
	case 2:
		mes "[Reno]";
		mes "Las Quest de Cazería están divididas por rango según el nivel del mob y la dificultad. Estos rangos son Bajo, intermedio y alto. Solo pueden ser tomadas una de cada rango cada 24 horas.";
		next;

		setarray .@quest_status[0],
			isbegin_quest(5162) + isbegin_quest(5163) + isbegin_quest(5164) + isbegin_quest(5165) + isbegin_quest(5166) + isbegin_quest(5167) + isbegin_quest(5168),
			isbegin_quest(5170) + isbegin_quest(5171) + isbegin_quest(5172) + isbegin_quest(5173),
			isbegin_quest(5175) + isbegin_quest(5176) + isbegin_quest(5177) + isbegin_quest(5178) + isbegin_quest(5179) + isbegin_quest(5180) + isbegin_quest(5181) + isbegin_quest(5182);
		setarray .@quest_index[0],5161,5169,5174;

		set .@select, select("Cazería de Rango Bajo [61-80]:Cazería de Rango Intermedio [80-99]:Cazería de Rango Alto [90+]:Abandonar mi actual Quest.") - 1;
		if (.@select < 3) {
			setarray .@min_level[0],60,80,90,100;
			if (BaseLevel < .@min_level[.@select]) {
				mes "[Reno]";
				mes "Lo siento pero para la cazería de "+.@type$[.@select]+" necesitarás ser nivel "+.@min_level[.@select]+" o superior. No pareces ser capaz de llevar a cabo esta solicitud.";
				close;
			}
			if (.@quest_status[.@select]) {
				mes "[Reno]";
				mes "Pareces estar bajo otra solicitud de cazería de "+.@type$[.@select]+". Si se te hace difícil... que te parece ^FF0000renunciar a esa cazería^000000?";
				close;
			}
			set .@playtime, checkquest(.@quest_index[.@select],PLAYTIME);
			if (.@playtime == 0 || .@playtime == 1) {
				mes "[Reno]";
				mes "Actualmente espero que termines la cazería de "+.@type$[.@select]+". Las tareas en cada colección solo se pueden completar una vez por día."; //custom translation
				close;
			}
			mes "[Reno]";
			mes "Has seleccionado a "+.@type$[.@select]+" para comenzar la cazería. Cuando termines, recibirás "+((.@features_iRO)?"10 ":"")+"^0000FF"+.@crystal$[.@select]+" Energy Crystals^000000, termina y habla conmigo.";
			next;
			mes "[Reno]";
			mes "Selecciona un monstruo que se te haga fácil de cazar, aqui está la lista.";
			next;
			switch (.@select) {
			case 0:
				set .@quest, select(
					"[Rango Bajo] Requiem",
					"[Rango Bajo] Bathory",
					"[Rango Bajo] Spring Rabbit",
					"[Rango Bajo] Sleeper",
					"[Rango Bajo] Evil Druid",
					"[Rango Bajo] Ground Petite",
					"[Rango Bajo] Clock"
				);
				break;
			case 1:
				set .@quest, select(
					"[Rango Intermedio] Siroma",
					"[Rango Intermedio] Dark Priest",
					"[Rango Intermedio] Stapo",
					"[Rango Intermedio] Solider"
				);
				break;
			case 2:
				set .@quest, select(
					"[Rango Avanzado] Desert Wolf",
					"[Rango Avanzado] Medusa",
					"[Rango Avanzado] Pinguicula",
					"[Rango Avanzado] Majoruros",
					"[Rango Alto] Raydric",
					"[Rango Alto] Naga",
					"[Rango Alto] Ancient Mummy",
					"[Rango Alto] Ancient Mimic"
				);
				if (.@quest > 4 && BaseLevel < .@min_level[3]) {
					mes "[Reno]";
					mes "Lo siento, pero requerimos experiencia para las cazerías de alto rango, nivel "+.@min_level[3]+" o superior. No pareces capacitado.";
					close;
				}
				break;
			}
			setquest .@quest_index[.@select] + .@quest;
			mes "[Reno]";
			mes "La solicitud para la cazería de "+.@type$[.@select]+" esta lista. Puedes ver los detalles en el Quest Log o Alt+U.";
			close;
		} else {
			setarray .@quest_count[0],7,4,8;
			for (set .@i,0; .@i<3; set .@i,.@i+1) {
				if (.@quest_status[.@i]) {
					mes "[Reno]";
					mes "Actualmente estás inscrito en la cazería de "+.@type$[.@i]+". Si la abandonas, el histoial se perderá. Realmente quieres dejarla?";
					next;
					set .@erase_quest, select("Si:No");
					switch (.@erase_quest) {
					case 1:
						for (set .@quest,.@quest_index[.@i]+1; .@quest<=.@quest_index[.@i]+.@quest_count[.@i]; set.@quest,.@quest+1) {
							if (isbegin_quest(.@quest))
								erasequest .@quest;
						}
						break;
					case 2:
						break;
					}
				}
			}
			switch (.@erase_quest) {
			case 0:
				mes "[Reno]";
				mes "No estás bajo ninguna cazería.";
				close;
			case 1:
				mes "[Reno]";
				mes "La solicitud ha sido retirada.";
				close;
			case 2:
				mes "[Reno]";
				mes "Por favor sigue en tu cazería.";
				close;
			}
		}
	case 3:
		mes "[Reno]";
		mes "Desea acumular cristales dentro de su cuerpo? a cambio recibirás un Buff?";
		next;
		set .@select, select("Acumular Energía?:[ Acumular Rough Energy Crystal ]:[ Acumular Purified Energy Crystal ]:[ Acumular High Energy Crystal ]");
		switch (.@select) {
		case 1:
			mes "[Reno]";
			mes "Los Energy Crystals se acumulan en tu cuerpo cuando cazas los monstruos que te pedimos.";
			next;
			mes "[Reno]";
			mes "Nuestra investigación muestra que algunos métodos permiten a los humanos acumular los cristales dentro del cuerpo en forma de un poderoso Buff.";
			next;
			mes "[Reno]";
			mes "Terminando tus ^0000FF3 horas^000000 de buff, podrás ganar una pequeña cantidad de cristales. Entonces, te interesa?";
			close;
		case 2:
			set .@playtime, checkquest(5225,PLAYTIME);
			set .@quest,5222;
			set .@min_level,60;
			set .@rate,1;
			set .@buff, SC_QUEST_BUFF1;
			break;
		case 3:
			set .@playtime, checkquest(5226,PLAYTIME);
			set .@quest,5223;
			set .@min_level,80;
			set .@rate,2;
			set .@buff, SC_QUEST_BUFF2;
			break;
		case 4:
			set .@playtime, checkquest(5227,PLAYTIME);
			set .@quest,5224;
			set .@min_level,90;
			set .@rate,3;
			set .@buff, SC_QUEST_BUFF3;
			break;
		}
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[Reno]";
			mes "Tuviste éxito? Mientras que almacenar otros cristales no tendrá mucho efecto en su cuerpo, almacenar el mismo puede hacerle daño."; //custom translation
			close;
		}
		if (BaseLevel < .@min_level) {
			mes "[Reno]";
			mes "Con un cuerpo como el tuyo, no puedes acumular "+.@crystal$[.@select-2]+" Energy Crystals. Debes tener un nivel mínimo de "+.@min_level+" o tu cuerpo se desvanecerá con tanto poder."; //custom translation
			close;
		}
		if (.@energy_buf00) {
			mes "[Reno]";
			mes "Ya estás en medio de una Acumulación. Me es imposible hacer que acumules dos diferentes tipos de cristales al mismo tiempo.";
			close;
		}
		if (isbegin_quest(.@quest) > 0) {
			erasequest .@quest;
			mes "[Reno]";
			mes "Ocurrió un error. Disculpa los inconvenientes."; //custom translation
			close;
		}
		mes "[Reno]";
		mes "Le tomará 3 horas a tu cuerpo acumular de 2 a 4 "+.@crystal$[.@select-2]+" Energy Crystals. Comenzamos?";
		next;
		if(select("Cancelar:Comenzar") == 1) {
			mes "[Reno]";
			mes "Recomiendo que acumules energías cuando tengas más tiempo.";
			close;
		}
		mes "[Reno]";
		mes "Es posible que sienta un ligero cosquilleo..";
		next;
		specialeffect2 EF_BASH3D;
		percentheal .@rate * -5,0;
		//consumeitem ??; //Keep_Connection_[.@rate]
		sc_start .@buff,10800000,.@rate;	// Atk/Matk + 5*rate (+5, +10, +15)
		setquest .@quest;
		mes "[Reno]";
		mes "- Puck -";
		next;
		mes "[Reno]";
		mes "Todo parece estar en orden. Empezó la acumulación de "+.@crystal$[.@select-2]+" Energy Crystals. Recuerda volver luego de 3 horas para obtener tus "+.@crystal$[.@select-2]+" Energy Crystals.";
		close;
	case 4:
		mes "[Reno]";
		mes "Quieres comprimir tus Energy Crystals?";
		next;
		switch(select("Que hace la Compresión?:Comprimir ^00CCCCRough^000000 a ^0066CCPurified^000000 Crystal:Comprimir ^0066CCPurified^000000 a ^0000CCHigh^000000 Crystal")) {
		case 1:
			mes "[Reno]";
			mes "¿Tienes curiosidad acerca de la compresión de Energy Crystal? Puedo comprimir cristales de bajo nivel en los más altos.";
			next;
			mes "[Reno]";
			mes "Siendo estos los valores..";
			mes "^00CCCC10 Rough Energy Crystals^000000 =^0066CC 1 Purified Energy Crystal^000000";
			mes "^0066CC Purified Energy Crystals^000000 =^0000CC 1 High Energy Crystal^000000";
			next;
			mes "[Reno]";
			mes "Si necesita cristales de mayor calidad, esta es una buena forma de obtenerlos, ya que los más bajos son más fáciles de obtener.";
			close;
		case 2:
			set .@crystal,6623;  //Rough_Energy_Crystal
			set .@crystal_,6624; //Purified_Energy_Crystal
			set .@rate,10;
			break;
		case 3:
			set .@crystal,6624;  //Purified_Energy_Crystal
			set .@crystal_,6625; //High_Purity_Energy_Xtal
			set .@rate,5;
			break;
		}
		set .@crystal_count, countitem(.@crystal);
		set .@crystal_get, .@crystal_count / .@rate;
		mes "[Reno]";
		mes sprintf("^FF0000%d %s^000000 = ^FF00001 %s^000000",.@rate,getitemname(.@crystal),getitemname(.@crystal_));
		next;
		mes "[Reno]";
		mes sprintf("%d %ss serán comprimidos a %d %ss.",.@crystal_count,getitemname(.@crystal),.@crystal_get,getitemname(.@crystal_));
		mes "Estás de acuerdo?";
		next;
		if(select("Comprimir:Detente") == 2) {
			mes "[Reno]";
			mes "Okay, por favor contactame si me necesitas en un futuro.";
			close;
		}
		while(1) {
			if (.@crystal_count >= .@rate) {  //custom translation
				delitem .@crystal,.@rate;
				getitem .@crystal_,1;
				set .@crystal_count, countitem(.@crystal);
				set .@crystal_get, .@crystal_count / .@rate;
				mes "[Reno]";
				mes "La compresión fue todo un éxito.";
				mes "Hay "+.@crystal_count+" Energy Crystals, los cuales puedes comprimir en "+.@crystal_get+" Energy Crystals. Quieres continuar?";
				next;
				if(select("Compress:Stop") == 2)
					break;
			} else {
				mes "[Reno]";
				mes "You don't have enough "+getitemname(.@crystal)+"s.";
				next;
				break;
			}
		}
		mes "[Reno]";
		mes "Compresión completa.";
		close;
	case 5:
		disable_items;
		mes "[Reno]";
		mes "¿Quieres intercambiar Energy Crystals por equipo? Seleccione el tipo de Energy Crystal que desea intercambiar.";
		next;
		setarray .@crystals[0], countitem(6623), countitem(6624), countitem(6625);
		switch(select("Ver Catálogo:["+.@crystals[0]+"] Rough Energy Crystal:["+.@crystals[1]+"] Purified Energy Crystal:["+.@crystals[2]+"] High Energy Crystal")) {
		case 1:
			mes "[Reno]";
			mes "Este es el catálogo sobre los equipos que te puedo actualizar. ^FF0000Los precios del catálogo varian un poco a lo que enrealidad ofrezco^000000. Por ser MysteryRO, bajamos los precios!.";
			close2;
			readbook 11060,1; //Energy_Xtal_Combi_Book
			end;

		// Variable descriptions
		// .@crystal     : Energy Crystal used
		// .@materials[] : equipment to upgrade
		// .@showslot[]  : display slots with equipment name (-1 to disable)
		// .@costs[]     : amount of Energy Crystals needed
		// .@rewards[]   : upgraded equipment
		case 2:
			set .@crystal,6623; //Rough_Energy_Crystal
			setarray .@materials[0], 5027, 5045, 5069, 5003, 2214, 5167, 5168, 5043;
			setarray .@showslot[0],    -1,   -1,   -1,   -1,   -1,    1,    1,   -1;
			setarray .@costs[0],      80,  80,  80,  80,  80,  100,  100,  60;
			setarray .@rewards[0],  18760,18761,18762,18763,18764,18769,18770,18771;
			break;
		case 3:
			set .@crystal,6624; //Purified_Energy_Crystal
			setarray .@materials[0], 2296, 5014, 5096, 2292,2615, 2355,2116, 2420, 2521, 5125;
			setarray .@showslot[0],    -1,   -1,   -1,   -1,  -1,    1,   1,    1,    1,    1;
			setarray .@costs[0],      100,  100,  100,  100, 150,  100, 100,  80,  80,  80;
			setarray .@rewards[0],  18772,18773,18774,18775,2956,15068,2183,22015,20710,18776;
			break;
		case 4:
			set .@crystal,6625; //High_Purity_Energy_Xtal
			setarray .@materials[0], 5019, 5162, 5025, 5022, 5353, 2423,2678,2679, 2355,2116, 2420, 2521, 5125;
			setarray .@showslot[0],    -1,    1,   -1,    0,    1,   -1,  -1,  -1,    1,   1,    1,    1,    1;
			setarray .@costs[0],      200,  300,  200, 500,  100,  200, 200, 200,   20,  20,   20,   20,   20;
			setarray .@rewards[0],  18765,18768,18766,18767,18767,22014,2957,2958,15068,2183,22015,20710,18776;
			break;
		}
		mes "[Reno]";
		mes "Selecciona el equipo a intercambiar por "+getitemname(.@crystal)+"s.";
		next;
		set .@crystal_count, countitem(.@crystal);
		set .@menu$,"";
		for (set .@i,0; .@i<getarraysize(.@materials); set .@i,.@i+1) {
			set .@item_name$, getitemname(.@materials[.@i]) + ((.@showslot[.@i] > -1)?"["+.@showslot[.@i]+"]":"");
			set .@menu$, .@menu$ + sprintf("%s (%d/%d):",.@item_name$,.@crystal_count,.@costs[.@i]);
		}
		set .@index, select(.@menu$) - 1;
		set .@material, .@materials[.@index];
		set .@item_name$, getitemname(.@material) + ((.@showslot[.@index] > -1)?"["+.@showslot[.@index]+"]":"");
		set .@cost, .@costs[.@index];
		set .@reward, .@rewards[.@index];

		//custom translations
		if (.@material == 2615) {  // Special case: Safety Ring
			if (countitem(2615) < 1 || countitem(2621) < 1 || countitem(2622) < 1 || countitem(2624) < 1 ||
			    countitem(2625) < 1 || countitem(2623) < 1 || countitem(2626) < 1 || countitem(.@crystal) < .@cost) {
				mes "[Reno]";
				mes "Insuficientes materiales para el Safety Ring[1]. Por favor ve tu inventario primero.";
				close;
			}
			mes "[Reno]";
			mes "Antes de evolucionar tu Safety Ring, lee estas ^FF0000precauciones^000000.";
			next;
			mes "[Reno]";
			mes "^FF0000Sobre el Safety Ring, todos los refinamientos y cartas de los materiales que pide, desaparecerán.^000000";
			next;
		} else {
			if (countitem(.@material) < 1 || countitem(.@crystal) < .@cost) {
				mes "[Reno]";
				mes "No tienes suficienes materiales, revisa de nuevo y vuelve.";
				close;
			}
			mes "[Reno]";
			mes "Antes de actualizar tu equipo, lee estas ^FF0000precauciones^000000.";
			next;
			mes "[Reno]";
			mes "^FF0000Al usar Energy Crystals para actualizar el equipamiento, se perderán todos los refinamientos, encantamientos y cartas anteriores.^000000";
			next;
		}
		mes "[Reno]";
		mes "Estás familiarizado con las ^FF0000precauciones^000000?";
		next;
		if(select("Lo estoy.:No.") == 2) {
			mes "[Reno]";
			mes "Ok, para la próxima entonces.";
			close;
		}
		mes "[Reno]";
		mes "Entonces empezará la actualización de "+.@item_name$+".";
		next;
		specialeffect2 EF_REPAIRWEAPON;
		progressbar "ffff00",2;
		delitem .@material,1;
		if (.@material == 2615) {
			delitem 2621,1; //Ring_
			delitem 2622,1; //Earring_
			delitem 2624,1; //Glove_
			delitem 2625,1; //Brooch_
			delitem 2623,1; //Necklace_
			delitem 2626,1; //Rosary_
		}
		delitem .@crystal,.@cost;
		getitem .@reward,1;
		mes "[Reno]";
		mes "La actualización fue exitosa. Tus Cristales ayudarán mucho a nuestra investigación.";
		close;
	case 6:
		mes "[Reno]";
		mes "¿Quieres donar Energy Crystals para obtener un Hat al azar??";
		next;
		switch(select("Donaciones?:Donar 180 Rough Energy Crystals.:Donar 280 Purified Energy Crystals.:Donar 400 High Energy Crystals.")) {
		case 1:
			mes "[Reno]";
			mes "Estamos recolectando Energy Crystals para que podamos investigar más combinaciones de hats para aventureros.";
			next;
			mes "[Reno]";
			mes "Gracias al apoyo de los aventureros, podemos utilizar estos cristales de energía para avanzar en nuestra investigación.";
			next;
			mes "[Reno]";
			mes "Así que recibimos un mínimo de 180 Rough Energy Crystals, 280 Purified Energy Crystals, y 400 High Energy Crystals.";
			if (!.@features_iRO) {
				next;
				mes "[Reno]";
				mes "Puede sentir que esto es una carga, pero por el bien de nuestra investigación, continúe."; //custom translation
				close;
			}
			mes "Por una modica donación a ECRA, te daremos un equipo al azar.";
			next;
			mes "[Reno]";
			mes "180 Rough Energy Crystals";
			mes "- New Mage Hat";
			mes "- New Magician Hat";
			mes "- New Kitsune Mask";
			mes "- New Joker Jester";
			mes "- New Bunny Band";
			mes "- New Munak Hat";
			mes "- New Bongun Hat";
			mes "- New Phantom Opera Mask";
			next;
			mes "[Reno]";
			mes "280 Purified Energy Crystals";
			mes "- Good Binoculars";
			mes "- Good Fin Helm";
			mes "- Good Assassin Mask";
			mes "- Good Welding Mask";
			mes "- Good Safety Ring";
			mes "- Good Angelic Protection";
			mes "- Good Angelic Guard";
			mes "- Good Angelic Cardigan";
			mes "- Good Angel's Reincarnation";
			next;
			mes "[Reno]";
			mes "400 High Energy Crystals";
			mes "- Enhanced Corsair";
			mes "- Enhanced Bone Helm";
			mes "- Enhanced Helm of Angel";
			mes "- Enhanced Variant Shoes";
			mes "- Enhanced Ring of Flame Lord";
			mes "- Enhanced Ring of Resonance";
			close;
		case 2:
			if (countitem(6623) < 180) {
				mes "[Reno]";
				mes "Gracias por tu aporte, pero necesitas 180 Rough Energy Crystals.";
				close;
			}
			delitem 6623,180; //Rough_Energy_Crystal
			set .@r, rand(1,17);
			     if (.@r <= 2)  getitem 18760,1; //Remodel_Wizardry_Hat
			else if (.@r <= 4)  getitem 18761,1; //Remodel_Magician_Hat
			else if (.@r <= 6)  getitem 18762,1; //Remodel_Mask_Of_Fox
			else if (.@r <= 8)  getitem 18763,1; //Remodel_Joker_Jester
			else if (.@r <= 10) getitem 18764,1; //Remodel_Bunny_Band
			else if (.@r <= 13) getitem 18769,1; //Remodel_Munak_Turban
			else if (.@r <= 16) getitem 18770,1; //Remodel_Bongun_Hat
			else                getitem 18771,1; //Remodel_Opera_Mask
			break;
		case 3:
			if (countitem(6624) < 280) {
				mes "[Reno]";
				mes "Gracias por tu aporte, pero necesitas 280 Purified Energy Crystals.";
				close;
			}
			delitem 6624,280; //Purified_Energy_Crystal
			set .@r, rand(1,18);
			     if (.@r <= 3)  getitem 18772,1; //Improved_Binoculars
			else if (.@r <= 6)  getitem 18773,1; //Improved_Fin_Helm
			else if (.@r <= 9)  getitem 18774,1; //Improved_Assassin_Mask
			else if (.@r <= 12) getitem 18775,1; //Improved_Welding_Mask
			else if (.@r <= 13) getitem 2956,1;  //Safety_Ring_
			else if (.@r <= 14) getitem 15068,1; //Im_Angel's_Protection
			else if (.@r <= 15) getitem 2183,1;  //Impr_Angel's_Safeguard
			else if (.@r <= 16) getitem 22015,1; //Impr_Angel's_Arrival
			else if (.@r <= 17) getitem 20710,1; //Impr_Angel's_Warmth
			else                getitem 18776,1; //Improved_Kiss_Of_Angel
			break;
		case 4:
			if (countitem(6625) < 400) {
				mes "[Reno]";
				mes "Gracias por tu aporte, pero necesitas 400 High Energy Crystals.";
				close;
			}
			delitem 6625,400; //High_Purity_Energy_Xtal
			set .@r, rand(1,33);
			     if (.@r <= 10) getitem 18765,1; //Enhanced_Corsair
			else if (.@r <= 20) getitem 18768,1; //Enhanced_Bone_Helm
			else if (.@r <= 25) getitem 18766,1; //Enhanced_Helm_Of_Angel
			else if (.@r <= 30) getitem 22014,1; //Enhanced_Variant_Shoes
			else if (.@r <= 31) getitem 2957,1;  //Good_Ring_Of_Flame_Lord
			else                getitem 2958,1;  //Good_Ring_Of_Resonance
			break;
		}
		mes "[Reno]";
		mes "Muchas gracias! Los Energy Crystals que donó serán de gran ayuda para nuestra investigación. Esta es tu recompensa por tu donación."; //custom translation
		close;
	case 7:  // iRO only
		mes "[Reno]";
		mes "Este es el catálogo de actualización del equipo que ofrece nuestra asociación de investigación ECRA, por favor échale un vistazo.";
		close2;
		readbook 11060,1; //Energy_Xtal_Combi_Book
		end;
	}

//callsub L_CheckHunting,<quest index>,<start quest>,<number of quests>,"<type>",<reward id>,<reward amount>;
L_CheckHunting:
	for (set .@quest,getarg(0)+1; .@quest<getarg(1)+getarg(2); set .@quest,.@quest+1) {
		if (checkquest(.@quest,HUNTING) == 2) {
			mes "[Reno]";
			mes "Terminada la cazería de "+getarg(3)+" exitosamente.";
			next;
			setquest getarg(0);
			erasequest .@quest;
			getitem getarg(4),getarg(5);
			mes "Recibes "+getarg(5)+" "+getitemname(getarg(4))+"s "+getarg(3)+" de recompensa.";
			close;
		}
OnInit:
	waitingroom "Cazerías, Buff & Hats",0;

	}
	return;
}
