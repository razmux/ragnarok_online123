//===== rAthena Script =======================================
//= Card Removal NPC
//===== By: ==================================================
//= TyrNemesis^
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================

prontera,193,171,4	script	Abuela Quita Cartas#eAcustom	78,{

	set .zenycost,200;    // base cost of the card remover services (in Zeny)
	set .percardcost,250;  // cost per card of the card remover services (in Zeny)
	set .faildestroy,0;      // should the card remover have a chance of failure that destroys items? (1=yes, 0=no)

	disable_items;
	mes "[Wise Old Woman]";
	mes "Buenos dias jovencito. Tengo el poder de remover las cartas que componen tu equipo. Te parece??";
	next;
	switch(select("Si, Hazlo.:Que pides a cambio?:No gracias.")) {
	case 1:
		mes "[Wise Old Woman]";
		mes "Muy bien. Cual es el item que quiere que examine?";
		next;

		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
			if( getequipisequiped(.@indices[.@i]) )
				set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@menu$, .@menu$ + ":";
		}
		set .@part, .@indices[ select(.@menu$) ];
		if(!getequipisequiped(.@part)) {
			mes "[Wise Old Woman]";
			mes "Jovencito... No estas usando algo como para remover alguna carta.";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[Wise Old Woman]";
			mes "Jovencito... No hay cartas en este item. No puedo hacer nada, Que tristeza.";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);

		if (!checkweight(1202,(.@cardcount+1))) {
			mes "^3355FFEspera un minuto!";
			mes "No puedo hacero,";
			mes "No puedo ofrecer mis servicios";
			mes "Porque tienes sobrepeso en tu inventario";
			mes "Guarda items";
			mes "En tu storage y vuelve otra vez~";
			close;
		}
		mes "[Wise Old Woman]";
		mes "Este item tiene " + .@cardcount + " cantidad de cartas. Para usar mi magia, voy a necesitar " + (.zenycost+(.@cardcount * .percardcost)) + " zeny, un ^0000FFStar Crumb^000000, y un ^0000FFYellow Gemstone^000000.";
		next;
		if(select("Bien, Hazlo.:No gracias.") == 2) {
			mes "[Wise Old Woman]";
			mes "Muy bien. Regresa si vuelves a necesitar mis servicios.";
			close;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[Wise Old Woman]";
			mes "Tu no tienes los items que necesito para realizar mi magia, joven. Vuelve cuando los tengas.";
			close;
		}
		mes "[Wise Old Woman]";
		mes "Es imposible que una veterena como yo falle!";
		next;
		switch(select("No se lo dudo, adios.:Si hagamoslo!.:Hmm bueno.")) {
		case 1:
			mes "[Wise Old Woman]";
			mes "Muy bien. Regresa de inmediato si buscas mis servicios.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[Wise Old Woman]";
		mes "Muy bien. Voy a comenzar.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1; //Star_Crumb
		delitem 715,1; //Yellow_Gemstone

		// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
		// First value = Total failure chance (item and cards destroyed)
		// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
		// Third value = Harmless failure chance (all that's lost is your investment)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Wise Old Woman]";
				mes "The process was a total failure. I am afraid the item and the cards were destroyed.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[Wise Old Woman]";
					mes "While I have managed to remove the cards from the item, they were destroyed in the process. The item, however, is okay.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Wise Old Woman]";
					mes "Most unfortunate. I succeeded at removing the cards, but the item itself was destroyed in the process.";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "[Wise Old Woman]";
			mes "I have failed to remove the cards. Luckily, however, both the item and the cards are still okay.";
			close;
		}
		next;
		successremovecards .@part;
		mes "[Wise Old Woman]";
		mes "El proceso esta compelto. Aca esta tu carta y tu item. Nos vemos.";
		close;
	case 2:
		mes "[Wise Old Woman]";
		mes "Requiero de "+callfunc("F_InsertComma",.zenycost)+" zeny, mas "+callfunc("F_InsertComma",.percardcost)+" por cada carta que remuevo. Adicionalmente, Necesito un star crumb y un yellow gemstone para hacer mi magia.";
		close;
	case 3:
		mes "[Wise Old Woman]";
		mes "Muy bien. Vuelva cuando necesite mis servicios.";
		close;
	}
}
