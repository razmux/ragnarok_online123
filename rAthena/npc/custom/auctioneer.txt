//===== rAthena Script =======================================
//= Auctioneer
//===== By: ==================================================
//= Daegaladh
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Custom compact auctioneer.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Optimized.
//============================================================

-	script	auction_calc	-1,{
	end;
OnInit:
	initnpctimer;
	end;

OnTimer29000:
	query_sql("SELECT COUNT(*) as total FROM `auction`;",$@auctions);
	end;

OnTimer30000:
	initnpctimer;
	end;
}

-	script	Subastador#dum::custom_auctioneer	-1,{

	mes "[Subastador]";
	mes "Bienvenid"+callfunc("F_SexMes","a","o")+" al Salón de Subastas.";
	mes "¿Desea ver nuestros artículos?";
	next;
	if (select("Sí:No") == 1) {
		mes "[Subastador]";
		mes "Muy bien.";
		mes "Por favor,";
		mes "eche un vistazo";
		mes "y vea los artículos que se ofrecen~";
		close2;
		openauction;
		end;
	}
	mes "[Subastador]";
	mes "De acuerdo.";
	mes "Si cambia de opinión,";
	mes "por favor, vuelva";
	mes "y eche un vistazo";
	mes "a nuestras subastas~";
	close;

OnInit:
	initnpctimer;
	end;

OnTimer30000:
	delwaitingroom;
	waitingroom "Hay "+$@auctions+" objeto"+(($@auctions == 1)?"":"s")+" a subasta.",0;
	initnpctimer;
	end;
}

// Duplicates
//============================================================
prontera,170,195,4	duplicate(custom_auctioneer)	Subastador#prt	47
moc_ruins,78,173,6	duplicate(custom_auctioneer)	Subastador#moc	98
yuno,129,116,0	duplicate(custom_auctioneer)	Subastador#jun	754
lighthalzen,205,169,6	duplicate(custom_auctioneer)	Subastador#lhz	833